;; This is the installation script for the SoftC Database Library.

Do SetupINSTALIT

QueAllFiles
[Number1] := QueSize

Dialog
	Welcome to the SoftC installation program.  This program will copy
	the files needed to install the SoftC Database Library on your
	system.  You will need approximately [Number1] bytes of available
	disk space to install the entire package including help file(s),
	and the sample code. Space requirements for the Windows DLL and the
	library source code (if they are provided) is included.
EndDialog

DetermineInstallationDrive RequireFixed

[Number1] := 5

[DIR1] := [InstallationDirectory]

[YesOrNo1] := Y
[YesOrNo2] := Y
[YesOrNo3] := Y
[YesOrNo4] := N

[Point1]

GetMenuChoice Highlight [Number1]
	SoftC Database Library Directory:        [DIR1]
	; Press <ENTER> to change the directory
	; for the SoftC Database Library files.
	Install Help Files:                      [YesOrNo1]
	; Press <ENTER> to toggle the help file installation.
	Install Windows DLL:                     [YesOrNo2]
	; Press <ENTER> to toggle the help file installation.
	Install Source Files:                    [YesOrNo3]
	; Press <ENTER> to toggle the source file installation.
	Automatically Overwrite Existing Files:  [YesOrNo4]
	; Press <ENTER> to toggle the overwrite inquiry.
	Begin Installation
	; Press <ENTER> to begin installation.
EndGetMenuChoice

InCase Choice is
	=1: Do ChangeSoftC      ;; change SoftC directory
			[Number1] := 1
	=2: [YesOrNo5] := [YesOrNo1]
			Do ToggleYN   			;; install help?
			[YesOrNo1] := [YesOrNo5]
			[Number1] := 2
	=3: [YesOrNo5] := [YesOrNo2]
			Do ToggleYN         ;; install Windows DLL?
			[YesOrNo2] := [YesOrNo5]
			[Number1] := 3
	=4: [YesOrNo5] := [YesOrNo3]
			Do ToggleYN         ;; install source?
			[YesOrNo3] := [YesOrNo5]
			[Number1] := 4
	=5: [YesOrNo5] := [YesOrNo4]
			Do ToggleYN         ;; ask before overwriting?
			[YesOrNo4] := [YesOrNo5]
			[Number1] := 5
	=6: Do Install  ;; begin installation
EndInCase

GoTo [Point1]

Procedure ChangeSoftC
	Dialog [Dir1] 50
	.cEnter directory where the SoftC Database Library is to be installed.
	EndDialog
EndProcedure

Procedure ToggleYN
	If [YesOrNo5] = N
		[YesOrNo5] := Y
	Else
		[YesOrNo5] := N
	EndIf
EndProcedure

Procedure Install
	If [YesOrNo1] = N
		UnQueFileGroup 3	;; help files
	EndIf
	If [YesOrNo2] = N
		UnQueFileGroup 80       ;; Windows DLL
	EndIf
	If [YesOrNo3] = N
		UnQueFileGroup 100      ;; source code, batch files, etc.
	EndIf
	If [YesOrNo4] = Y
		SetReplacementInquiry Off ;; don't ask before overwriting
	EndIf
	GetQuedFiles
	Dialog PressAKey
		.cPlease check the README.1ST file found in
		.c[DIR1]\HELP
		.cfor last-minute information that
		.cmay not be included in the manual.
	EndDialog
	SoLong
EndProcedure


Procedure SetupINSTALIT
	SetBackgroundCharTo B0
	SetShadowAttrTo DarkGrayOnBlack
	SetScreenAttrTo WhiteOnBlue
	SetTopLineAttrTo RedOnWhite
	SetTopLineTo " Installing the SoftC Database Library "
	SetBottomLineAttrTo CyanOnWhite
	SetBottomLineTo " Use <CtrlX> to quit. "
	SetExitMessageTo  Thank you for choosing the SoftC Database Library!
EndProcedure

EndScript
